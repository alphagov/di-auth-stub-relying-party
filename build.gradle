import org.gradle.api.JavaVersion

buildscript {
    repositories {
        maven {
            url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
        }
    }
}

plugins {
    id "com.diffplug.spotless" version "6.3.0"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

group 'uk.gov.di'

repositories {
    maven {
        url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
    }
}

dependencies {
    implementation "com.sparkjava:spark-core:2.9.3",
            "com.sparkjava:spark-template-mustache:2.7.1",
            "com.nimbusds:oauth2-oidc-sdk:9.10.1",
            "org.slf4j:slf4j-simple:1.7.36",
            "org.apache.httpcomponents:httpclient:4.5.13",
            "io.pivotal.cfenv:java-cfenv:2.4.0"

    implementation('org.eclipse.jetty:jetty-server') {
        version {
            strictly '9.4.43.v20210629'
        }
    }

    testImplementation "junit:junit:4.13.2"
}

spotless {
    ratchetFrom 'origin/main'

    java {
        target "**/*.java"
        googleJavaFormat("1.13.0").aosp()
        importOrder "", "javax", "java", "\\#"
    }

    groovyGradle {
        target "**/*.gradle"
        greclipse().configFile("tools/spotless-gradle.properties")
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

run {
    debugOptions {
        enabled = true
        port = 8083
        server = true
        suspend = false
    }
    file('.env').exists() && file('.env').readLines().each() {
        if (!it.isEmpty() && !it.startsWith('#') && it.contains('=')) {
            def (key, value) = it.tokenize('=')
            environment key, value
        }
    }
}

mainClassName = 'uk.gov.di.App'
