import org.gradle.api.JavaVersion

buildscript {
    repositories {
        maven {
            url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
        }
    }
}

plugins {
    id "com.diffplug.spotless" version "6.11.0"
    id "org.sonarqube" version "4.2.1.3168"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

group 'uk.gov.di'

repositories {
    maven {
        url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
    }
}

dependencies {
    implementation "com.sparkjava:spark-core:2.9.3",
            "com.sparkjava:spark-template-mustache:2.7.1",
            "com.nimbusds:oauth2-oidc-sdk:11.9",
            "org.slf4j:slf4j-simple:1.7.36",
            "org.apache.httpcomponents:httpclient:4.5.13",
            "io.pivotal.cfenv:java-cfenv:2.4.0",
            'net.minidev:json-smart:2.5.0',
            'com.fasterxml.jackson.core:jackson-databind:2.15.3',
            implementation('org.eclipse.jetty:jetty-server') {
                version {
                    strictly '10.0.17'
                }
            }
    testImplementation "junit:junit:4.13.2"
}

spotless {
    java {
        target "**/*.java"
        googleJavaFormat("1.13.0").aosp()
        importOrder "", "javax", "java", "\\#"
    }

    groovyGradle {
        target "**/*.gradle"
        greclipse().configFile("tools/spotless-gradle.properties")
    }
}

sonar {
    properties {
        property "sonar.projectKey", "alphagov_di-auth-stub-relying-party"
        property "sonar.organization", "alphagov"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

run {
    debugOptions {
        enabled = true
        port = 8083
        server = true
        suspend = false
    }
    file('.env').exists() && file('.env').readLines().each() {
        if (!it.isEmpty() && !it.startsWith('#') && it.contains('=')) {
            def (key, value) = it.tokenize('=')
            environment key, value
        }
    }
}

task zipRouteService(type: Zip) {
    from 'nginx/'
    include '*'
    archiveName 'di-auth-stub-rp-route-service.zip'
}

mainClassName = 'uk.gov.di.App'
